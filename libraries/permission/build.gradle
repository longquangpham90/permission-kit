apply from: '../android_libs.gradle'
def getPropertiesFile = { path ->
    Properties properties = new Properties()
    properties.load(new FileInputStream(file(path)))
    return properties
}

def properties = getPropertiesFile("../../local.properties")
def githubUsername = properties.getProperty("GITHUB_USERNAME") ?: ""
def githubToken = properties.getProperty("GITHUB_TOKEN") ?: ""

android {

    namespace "com.studio.permission"

    defaultConfig {
        versionCode 3
        versionName "1.1.1"
    }

    flavorDimensions = ["environment"]
    productFlavors {
        dev {
            dimension "environment"
        }
        qa {
            dimension "environment"
        }
        prod {
            dimension "environment"
        }
    }
}

dependencies {
}

def libName = 'permission'

publishing {
    publications {
        "mavenAar${libName}"(MavenPublication) {
            groupId = "com.studio.${libName}"
            artifactId = "${libName}-kit"
            version = android.defaultConfig.versionName
            artifact(file("$buildDir/outputs/aar/${libName}.aar"))
            artifact sourcesJar
            artifact dokkaJar

            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode("dependencies")
                    def dependencyNode = dependenciesNode.appendNode("dependency")
                    dependencyNode.appendNode("groupId", "com.studio.${libName}")
                    dependencyNode.appendNode("artifactId", "${libName}-kit")
                    dependencyNode.appendNode("version", android.defaultConfig.versionName)
                    dependencyNode.appendNode("scope", "runtime")
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            url uri('../../repository')
        }
        maven {
            url = uri("https://maven.pkg.github.com/longquangpham90/permission-kit")
            credentials {
                username = githubUsername
                password = githubToken
            }
        }
    }
}